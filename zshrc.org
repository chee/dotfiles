#+TITLE: ~/.zshrc
* History

#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
# save each command's beginning timestamp and the duration to the history file
setopt extended_history

zstyle :compinstall filename '/home/kevin/.zshrc'

# autocompletion of command line switches for aliases
setopt COMPLETE_ALIASES

# in order to use #, ~ and ^ for filename generation grep word
# *~(*.gz|*.bz|*.bz2|*.zip|*.Z) -> searches for word not in compressed files
# don't forget to quote '^', '~' and '#'!
setopt extended_glob

# display PID when suspending processes as well
setopt longlistjobs
#+END_SRC
* Prompt
Custom prompt with snippets of code taken from various places.

#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  # If set, parameter expansion, command substitution and arithmetic expansion are performed in prompts. Substitutions within prompts do not affect the command status.
  setopt prompt_subst

  autoload -Uz compinit promptinit vcs_info
  compinit
  promptinit

  # from https://stackoverflow.com/a/12935606
  # mimics git status -s
  # first M is staged
  # second M is unstaged
  # question marks are untracked
  # symbols:  ±  ➦ ✘ ⚡ ⚙
  zstyle ':vcs_info:*' stagedstr 'M' 
  zstyle ':vcs_info:*' unstagedstr 'M' 
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' actionformats '%F{5}[%F{4}%b%F{3}|%F{1}%a%F{5}]%f '
  zstyle ':vcs_info:*' formats \
    '%F{242}[%b] %F{2}%c%F{3}%u'
  zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
  zstyle ':vcs_info:*' enable git 
  +vi-git-untracked() {
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
    [[ $(git ls-files --other --directory --exclude-standard | sed q | wc -l | tr -d ' ') == 1 ]] ; then
    hook_com[unstaged]+='%F{1}??%f'
  fi
  }

  precmd() {
      vcs_info
  }

  # from robbyrussel
  # show a green error if the exit code is 0, otherwise show a red arrow
  # alternate arrows: ➜
  ret_status="%(?:%F{green}❯%f:%F{red}❯%f)"

  PROMPT=' ${ret_status} %B%F{cyan}%6~%f%b  '
  RPROMPT='${vcs_info_msg_0_}'
#+END_SRC

* Completion
Autocompletion of privileged environments in privileged commands, e.g. sudo
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*' menu select
#+END_SRC

* Aliases
Start an =ssh-agent= that is useable by all programs in the X session.
Without this, Emacs won't be able to access the =ssh-agent= if its started on the terminal.
(Alternatively, you could fix this by creating a systemctl service, but that wasn't working for me.)
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  alias sx='ssh-agent startx'
#+END_SRC

Pacman aliases
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  alias pac='sudo pacman'
  alias pacs='pac -S'
  alias pacu='pac -Syu'
  alias pacy='pac -Sy'
  alias pacss='pac -Ss'
  alias pacq='pac -Qs'
#+END_SRC

* Paths
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  export PATH="$HOME/bin:$HOME/.local/bin:/usr/local/bin:$HOME/.node_modules/bin:$(ruby -e 'print Gem.user_dir')/bin:$home/composer/vendor/bin:$PATH"
  export npm_config_prefix=~/.node_modules
#+END_SRC

* Plugins
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  # autosuggestions
  # installed with pacman
  source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

  # syntax highlighting
  # installed with pacman
  source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

  # history search
  # installed with pacman
  source /usr/share/zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh
#+END_SRC

* Fixes
** Delete Key
The delete key by default inserts a ~, this makes it act like it should
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  # fix delete key
  bindkey "^[[3~" delete-char
#+END_SRC

** Tmux Workaround
Workaround for handling TERM variable in multiple tmux sessions properly from http://sourceforge.net/p/tmux/mailman/message/32751663/ by Nicholas Marriott
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  if [[ -n ${TMUX} && -n ${commands[tmux]} ]];then
          case $(tmux showenv TERM 2>/dev/null) in
                  ,*256color) ;&
                  TERM=rxvt-unicode-256color)
                          TERM=screen-256color ;;
                  ,*)
                          TERM=screen
          esac
  fi
#+END_SRC
